import itertools

#  add 10K cutup as assemblies for prodigal to predict genes for
config["prodigal_rules"]["assemblies"] = {a + "_10K": "concoct/{a}/cutup/contigs_10K.fasta".format(a=a) for a in config["concoct_rules"]["assemblies"]}
config["prodigal_rules"]["assemblies"].update(config["concoct_rules"]["assemblies"])

#  add prodigal predicted genes as query for rpsblast
config["rpsblast_rules"]["query_aas"] = {a: "annotation/prodigal/default-meta/{a}/proteins/proteins.faa".format(a=a) for a in config["prodigal_rules"]["assemblies"]}

#  add prodigal predicted genes as query for hmmer
config["hmmer_rules"]["query_aas"] = config["rpsblast_rules"]["query_aas"]

rule concoct_eval_cog_table:
    """
    Generate COG table from rpsblast output and concoct binning results
    """
    input:
        clust="concoct/{assembly}/output/{concoct_params}/clustering.csv",
        rpsblast="blast/rpsblast/default-concoct/cog/{assembly}_10K/rpsblast.out"
    output:
        "concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg.tsv"
    shell:
        """
        {config[concoct_rules][load_env]}
        python {config[concoct_rules][scripts_dir]}/COG_table.py \
            -b {input.rpsblast} \
            -m {config[concoct_rules][scripts_dir]}/../scgs/scg_cogs_min0.97_max1.03_unique_genera.txt \
            -c {input.clust} \
            --cdd_cog_file {config[concoct_rules][scripts_dir]}/../scgs/cdd_to_cog.tsv \
            > {output}
        """

rule concoct_eval_cog_table_nocutup:
    """
    Generate COG table from rpsblast output and concoct binning results
    """
    input:
        clust="concoct/{assembly}/output/{concoct_params}/clustering_nocutup.csv",
        rpsblast="blast/rpsblast/default-concoct/cog/{assembly}/rpsblast.out"
    output:
        "concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg_nocutup.tsv"
    shell:
        """
        {config[concoct_rules][load_env]}
        python {config[concoct_rules][scripts_dir]}/COG_table.py \
            -b {input.rpsblast} \
            -m {config[concoct_rules][scripts_dir]}/../scgs/scg_cogs_min0.97_max1.03_unique_genera.txt \
            -c {input.clust} \
            --cdd_cog_file {config[concoct_rules][scripts_dir]}/../scgs/cdd_to_cog.tsv \
            > {output}
        """

rule concoct_extract_all_bins:
    input:
        asms="concoct/{assembly}/cutup/contigs_10K.fasta",
        clustering="concoct/{assembly}/output/{concoct_params}/clustering.csv"
    output:
        dynamic("concoct/{assembly}/bins/{concoct_params}/{{bin_name}}.fa")
    params:
        extract_script=config["concoct_rules"]["scripts_dir"] + "/extract_fasta_bins.py"
    shell:
        """
            {config[concoct_rules][load_env]
            python {params.extract_script} {input.asms} {input.clustering} \
                 --output_path concoct/{wildcards.assembly}/bins/{wildcard.params}
        """
 
rule concoct_extract_bins_all:
    input:
        dynamic(expand("concoct/{assembly}/bins/{concoct_params}/{{bin_name}}.fa",
                    assembly=config["concoct_rules"]["assemblies"],
                    concoct_params=config["concoct_rules"]["concoct_params"]))

rule concoct_extract_approved_scg_bins:
    input:
        scg_tsvs=expand("concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg.tsv",
            assembly=sorted(config["concoct_rules"]["assemblies"]),
            concoct_params=sorted(config["concoct_rules"]["concoct_params"])),
        asms=expand("concoct/{assembly}/cutup/contigs_10K.fasta",
            assembly=sorted(config["concoct_rules"]["assemblies"]),
            concoct_params=sorted(config["concoct_rules"]["concoct_params"]))
    output:
        dynamic("concoct/approved_scg_bins/{cluster_name}.fa")
    params:
        names=expand("{assembly}_{concoct_params}",
            assembly=sorted(config["concoct_rules"]["assemblies"]),
            concoct_params=sorted(config["concoct_rules"]["concoct_params"])),
        groups=expand("{assembly}",
            assembly=sorted(config["concoct_rules"]["assemblies"]),
            concoct_params=sorted(config["concoct_rules"]["concoct_params"]))
    shell:
        """
            {config[concoct_rules][load_env]}
            python {config[concoct_rules][scripts_dir]}/extract_scg_bins.py \
                --output_folder concoct/approved_scg_bins \
                --scg_tsvs {input.scg_tsvs} \
                --fasta_files {input.asms} \
                --names {params.names} \
                --groups {params.groups} \
                --max_missing_scg 5 \
                --max_multicopy_scg 2
         """

def duplicate_items(nr_items, items):
    """A list where each item in items is present nr_items times. 

    The order is preserved from items"""
    return itertools.chain([nr_items*[item]for item in items])


rule concoct_extract_approved_scg_bins_nocutup:
    input:
        scg_tsvs=expand("concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg_nocutup.tsv",
            assembly=sorted(config["concoct_rules"]["assemblies"]),
            concoct_params=sorted(config["concoct_rules"]["concoct_params"])),
        asms= duplicate_items(len(config["concoct_rules"]["concoct_params"]), [config["concoct_rules"]["assemblies"][assembly] for assembly in sorted(config["concoct_rules"]["assemblies"].keys())])
    output:
        dynamic("concoct/approved_scg_bins_nocutup/{cluster_name}.fa")
    params:
        names=expand("{assembly}_{concoct_params}",
            assembly=sorted(config["concoct_rules"]["assemblies"]),
            concoct_params=sorted(config["concoct_rules"]["concoct_params"])),
        groups=expand("{assembly}",
            assembly=sorted(config["concoct_rules"]["assemblies"]),
            concoct_params=sorted(config["concoct_rules"]["concoct_params"]))
    shell:
        """
            {config[concoct_rules][load_env]}
            python {config[concoct_rules][scripts_dir]}/extract_scg_bins.py \
                --output_folder concoct/approved_scg_bins_nocutup \
                --scg_tsvs {input.scg_tsvs} \
                --fasta_files {input.asms} \
                --names {params.names} \
                --groups {params.groups} \
                --max_missing_scg 5 \
                --max_multicopy_scg 2
         """

rule concoct_extract_approved_scg_bins_all:
    input:
        dynamic("concoct/approved_scg_bins/{cluster_name}.fa")


rule concoct_extract_approved_scg_bins_all_nocutup:
    input:
        dynamic("concoct/approved_scg_bins_nocutup/{cluster_name}.fa")

rule concoct_dnadiff_dist_matrix:
    """Get distance matrix from approved SCG bins"""
    input:
        clusters=dynamic("concoct/approved_scg_bins/{cluster_name}.fa")
    output:
        "concoct/dnadiff_dist_matrix/dist_matrix.tsv",
        "concoct/dnadiff_dist_matrix/hclust_heatmap.pdf",
        "concoct/dnadiff_dist_matrix/hclust_dendrogram.pdf"
    params:
        tmp_dir = config["concoct_rules"]["dnadiff_tmp_dir"]
    run:
        sorted_input = sorted(input.clusters)
        shell("""
        {config[concoct_rules][load_env]}
        mkdir -p {params.tmp_dir}
        python {config[concoct_rules][scripts_dir]}/dnadiff_dist_matrix.py \
            {params.tmp_dir} {sorted_input}
        mv {params.tmp_dir}/dist_matrix.tsv {output[0]} 
        mv {params.tmp_dir}/hclust_heatmap.pdf {output[1]}
        mv {params.tmp_dir}/hclust_dendrogram.pdf {output[2]}
        """)


rule concoct_dnadiff_dist_matrix_report:
    input:
        dnadiff_output=rules.concoct_dnadiff_dist_matrix.output,
        readme_rst=glob.glob("report/concoct/dnadiff_dist_matrix/README.rst")
    output:
        "report/concoct/dnadiff_dist_matrix/index.html"
    params:
        readme_html="report/concoct/dnadiff_dist_matrix/README.html"
    shell:
        """
        cp --parents {input.dnadiff_output} report/
        (
            echo '<html><head><style>body {{ text-align: center }}</style></head><body>'
            for p in $(for i in {input.dnadiff_output}; do basename $i; done | sort); do
                echo "<a href='$p'>$p</a><br />"
            done
            for f in {input.readme_rst}; do
                echo "<iframe style='width: 80%; height: 100%;' frameBorder=0 src='README.html'></iframe>"
                rst2html.py $f > {params.readme_html}
            done
            echo '</body></html>'
        ) > {output}
        """

rule concoct_eval_cog_plot_merged:
    """
    Plot COGs using COG table
    """
    input:
        "concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg_merged.tsv"
    output:
        "concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg_merged.pdf"
    shell:
        """
        {config[concoct_rules][load_env]}
        Rscript {config[concoct_rules][scripts_dir]}/COGPlot.R \
            -s {input} \
            -o {output}
        """


rule concoct_eval_cog_plot:
    """
    Plot COGs using COG table
    """
    input:
        "concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg.tsv"
    output:
        "concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg.pdf"
    shell:
        """
        {config[concoct_rules][load_env]}
        Rscript {config[concoct_rules][scripts_dir]}/COGPlot.R \
            -s {input} \
            -o {output}
        """


rule concoct_eval_cog_plot_all:
    input:
        expand("concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg.pdf",
            assembly=config["concoct_rules"]["assemblies"],
            concoct_params=config["concoct_rules"]["concoct_params"])


rule concoct_eval_cog_report:
    input:
        expand("concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg.pdf",
            assembly=config["concoct_rules"]["assemblies"],
            concoct_params=config["concoct_rules"]["concoct_params"])
    output:
        "report/concoct/cog_plots.html"
    shell:
        """
        cp --parents {input} report/
        (
            echo '<html><head><style>body {{ text-align: center }}</style></head><body>'
            for p in $(for i in {input}; do echo $i | cut -d/ -f2-; done | sort); do
                echo "<a href='$p'>$p</a><br />"
            done
            echo '</body></html>'
        ) > {output}
        """

rule concoct_eval_report:
    input:
        cog_html=rules.concoct_eval_cog_report.output,
        dnadiff_html=rules.concoct_dnadiff_dist_matrix_report.output
    output:
        "report/concoct/index.html"
    shell:
        """
        (
            echo '<html><head><style>body {{ text-align: center }}</style></head><body>'
            echo "<a href='cog_plots.html'>COG Plots</a><br />"
            echo "<a href='dnadiff_dist_matrix/index.html'>DNA Diff matrix as constructed with MUMmer</a><br />"
            echo '</body></html>'
        ) > {output}
        """

rule concoct_eval_cog_report_flashy:
    input:
        cog_plots=expand("concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg.pdf",
            assembly=config["concoct_rules"]["assemblies"],
            concoct_params=config["concoct_rules"]["concoct_params"])
    output:
        html="report/concoct/cog_plots_flashy.html"
    run:
        dict_cp = {"{a}-{cp}".format(a=cp.split("/")[1],cp=cp.split("/")[4]):cp for cp in input.cog_plots}
        cp_ids = "\n".join(["- " + cp + "_" for cp in sorted(dict_cp.keys())])
        report("""
        =========
        SCG Plots
        =========
        {cp_ids}
        """, output.html, **dict_cp)


rule concoct_eval_cog_table_merged:
    """
    Generate COG table from rpsblast output and concoct binning results
    """
    input:
        clust="concoct/{assembly}/output/{concoct_params}/clustering_merged.csv",
        rpsblast="blast/rpsblast/default-concoct/cog/{assembly}/rpsblast.out"
    output:
        "concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg_merged.tsv"
    shell:
        """
        {config[concoct_rules][load_env]}
        python {config[concoct_rules][scripts_dir]}/COG_table.py \
            -b {input.rpsblast} \
            -m {config[concoct_rules][scripts_dir]}/../scgs/scg_cogs_min0.97_max1.03_unique_genera.txt \
            -c {input.clust} \
            --cdd_cog_file {config[concoct_rules][scripts_dir]}/../scgs/cdd_to_cog.tsv \
            > {output}
        """

rule concoct_eval_merged:
    """
    Plot COGs using COG table for only merged
    """
    input:
        expand("concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg_merged.pdf",
                assembly=config["concoct_rules"]["assemblies"],
                concoct_params = config["concoct_rules"]["concoct_params"])

rule concoct_eval_all:
    """
    Plot COGs using COG table for both merged and cutup
    """
    input:
        expand("concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg_merged.pdf",
                assembly=config["concoct_rules"]["assemblies"],
                concoct_params = config["concoct_rules"]["concoct_params"]),
        expand("concoct/{assembly}/evaluation/scg/{concoct_params}/clustering_scg.pdf",
                assembly=config["concoct_rules"]["assemblies"],
                concoct_params = config["concoct_rules"]["concoct_params"])

