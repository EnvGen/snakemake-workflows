rule run_eggnog:
    input:
        "annotation/prodigal/{parameters}/{assembly}/proteins/proteins.faa"
    output:
        tsv = "annotation/EggNOG/{parameters}/{assembly}/hmm_result.tsv",
        txt = "annotation/EggNOG/{parameters}/{assembly}/hmm_report.txt"
    params:
        egg_nog_db = config["eggnog_rules"]["db_path"],
        hmmsearch_params = config["eggnog_rules"]["hmmsearch_params"]
    shell: 
        """{config[eggnog_rules][load_env]};
           input_basename=`basename {input}`;
           database_basename=`basename {params.egg_nog_db}`;
           cp {params.egg_nog_db}.h3* $SNIC_TMP/;
           cp {input} $SNIC_TMP/;
           hmmsearch -o {output.txt} {params.hmmsearch_params} --tblout {output.tsv} $SNIC_TMP/$database_basename $SNIC_TMP/$input_basename""" 

rule run_eggnog_all:
    input: expand("annotation/EggNOG/{parameters}/{assembly}/hmm_result.tsv", parameters=config["prodigal_rules"]["prodigal_params"], assembly=config["prodigal_rules"]["assemblies"])

rule eggnog_filter_output:
    input:
        "annotation/EggNOG/{parameters}/{assembly}/hmm_result.tsv"
    output:
        filtered_result = "annotation/EggNOG/{parameters}/{assembly}/hmm_result.filtered.tsv",
        header_comments = "annotation/EggNOG/{parameters}/{assembly}/hmm_result.filtered.head"
    params:
        max_eval = config["eggnog_rules"]["max_eval_first_filtering"]
    shell:
        """
        grep '^#' {input} > {output.header_comments};
        grep -v '^#' {input} | awk '{{if ($5 < {params.max_eval}) print}}' > {output.filtered_result}
        """

rule eggnog_collate_annotations:
    input:
        expand("annotation/EggNOG/{parameters}/{assembly}/hmm_result.filtered.tsv", parameters=config["prodigal_rules"]["prodigal_params"], assembly=config["prodigal_rules"]["assemblies"])
    output:
        "annotation/EggNOG/summary_annotation/all.EggNOG.tsv"
    shell:
        """
            sort --merge --key=3,3 {input} > {output}
        """
